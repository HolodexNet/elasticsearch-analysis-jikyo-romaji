// elasticsearch-analysis-jikyo-romaji

// A workaround for 7.5: https://github.com/elastic/elasticsearch/issues/49787#issuecomment-562720655
import org.elasticsearch.gradle.testclusters.TestClustersRegistry
import org.elasticsearch.gradle.testclusters.TestClustersPlugin

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'org.elasticsearch.gradle:build-tools:7.5.2'
    }
}

apply plugin: 'elasticsearch.esplugin'

// 7.5.0: (skipped) https://github.com/elastic/elasticsearch/issues/49787
def ver = [
    plugin: '7.5.2',
    es:     '7.5.2',
    lucene: '8.3.0', // https://github.com/elastic/elasticsearch/blob/master/buildSrc/version.properties
    romaji: '0.0.4',
]

esplugin {
    name 'analysis-jikyo-romaji'
    version ver.plugin
    description 'A token filter to romanize Japanese hiragana/katakana charcters.'
    classname 'com.github.jikyo.elasticsearch.plugin.analysis.romaji.AnalysisRomajiPlugin'
    licenseFile rootProject.file('LICENSE.txt')
    noticeFile rootProject.file('NOTICE.txt')
}

dependencies {
    implementation 'org.apache.lucene:lucene-core:' + ver.lucene
    implementation 'org.apache.lucene:lucene-analyzers-common:' + ver.lucene
    implementation 'org.elasticsearch:elasticsearch:' + ver.es

    compile 'com.github.jikyo:romaji:' + ver.romaji
}

configurations.restSpec.withDependencies { dependencies ->
    dependencies.clear()
    dependencies.add(project.dependencies.create("org.elasticsearch:rest-api-spec:${ver.es}"))
}

dependencyLicenses.enabled = false
thirdPartyAudit.enabled = false
licenseHeaders.enabled = false

// A workaround for 7.5: https://github.com/elastic/elasticsearch/issues/49787#issuecomment-562720655
TestClustersRegistry registry = project.rootProject.extensions.create("testClustersRegistry", TestClustersRegistry)
TestClustersPlugin.configureClaimClustersHook(project.gradle, registry)
TestClustersPlugin.configureStartClustersHook(project.gradle, registry)
TestClustersPlugin.configureStopClustersHook(project.gradle, registry)
